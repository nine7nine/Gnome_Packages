# Maintainer:  Jordan Johnston <johnstonljordan AT gmail DOT com>
# Contributoy: Peter Weber <peter.weber@mailbox.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
# Contributor: Fernando Fernandez <fernando@softwareperonista.com.ar>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-terminal-lefty
_pkgname=gnome-terminal
pkgver=3.46.4
pkgrel=1
pkgdesc="The GNOME Terminal Emulator with transparency && Southpaw Scrollbar"
url="https://wiki.gnome.org/Apps/Terminal"
arch=(x86_64)
license=(GPL)
depends=(vte3 gsettings-desktop-schemas)
makedepends=(docbook-xsl libnautilus-extension gnome-shell yelp-tools meson)
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
options=(debug)
_commit=44d55c8626d609d61905ef5cf11f0af2d7b7acd4  # tags/3.46.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-terminal.git#commit=$_commit"
        transparency.patch
        gnome-terminal-lefty.patch)
sha256sums=('SKIP'
            '04743ff919d5fc9304e2e6c69297139864f68ec275ba323b90e53e48cb6c9257'
            '893113f9ac473901a4ff919a885db36a98076f65d6fc9a6c2657e6e06aabd88c')

pkgver() {
  cd gnome-terminal
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gnome-terminal
  patch -Np1 -i ../transparency.patch
  patch -Np1 -i ../gnome-terminal-lefty.patch
}

build() {
  local meson_options=(
    -D b_lto=false
  )

  arch-meson gnome-terminal build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
