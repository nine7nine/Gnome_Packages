# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell_n7n
_pkgname=gnome-shell
pkgver=44.2
pkgrel=1
epoch=1
pkgdesc="Next generation desktop shell"
url="https://wiki.gnome.org/Projects/GnomeShell"
arch=(x86_64)
license=(GPL)
depends=(
  accountsservice
  gcr-4
  gjs
  gnome-autoar
  gnome-session
  gnome-settings-daemon
  gsettings-desktop-schemas
  gtk4
  libadwaita
  libcanberra-pulse
  libgdm
  libgweather-4
  libibus
  libnma-gtk4
  libsecret
  libsoup3
  mutter
  unzip
  upower
)
makedepends=(
  asciidoc
  bash-completion
  evolution-data-server
  git
  gnome-control-center
  gobject-introspection
  gtk-doc
  meson
  sassc
)
if [ -n "$_enable_check" ]; then
  checkdepends=(
    appstream-glib
    python-dbusmock
    xorg-server-xvfb
  )
fi
optdepends=(
  'evolution-data-server: Evolution calendar integration'
  'gnome-bluetooth-3.0: Bluetooth support'
  'gnome-control-center: System settings'
  'gnome-disk-utility: Mount with keyfiles'
  'gst-plugin-pipewire: Screen recording'
  'gst-plugins-good: Screen recording'
  'power-profiles-daemon: Power profile switching'
  'switcheroo-control: Multi-GPU support'
)
groups=(gnome)
provides=(gnome-shell gnome-shell=$pkgver gnome-shell=$epoch:$pkgver)
conflicts=(gnome-shell)
_commit=071a47ba27d7117f56fd965ad2a9675d1ab823f6 # tags/44.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "GS_transparency.patch"
        "no_Overview_left_Clock.patch")
sha256sums=('SKIP'
            'SKIP'
            '408386b4bab0d9012f8a9d38d6e498e9374377f74033871b00d88765c578926f'
            'e57a0a1a6c91461e4b998da01d8458247990a8c0c919b9ad54d79375472ab3e6')

pkgver() {
  cd gnome-shell
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gnome-shell

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
  
  patch -p1 < "$srcdir/no_Overview_left_Clock.patch"
  patch -p1 < "$srcdir/GS_transparency.patch"
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson $_pkgname build \
    -D gtk_doc=false \
    -D tests=false
  meson compile -C build
}

_check() (
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/rdir}"
  export XDG_RUNTIME_DIR

  meson test -C build --print-errorlogs -t 3
)

if [ -n "$_enable_check" ]; then
  check() {
    dbus-run-session xvfb-run -s '-nolisten local +iglx -noreset' \
      bash -c "$(declare -f _check); _check"
  }
fi

package() {
  depends+=(libmutter-12.so)
  install=gnome-shell.install
  meson install -C build --destdir "$pkgdir"
}
