# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell_n7n
_pkgname=gnome-shell
pkgver=44.1
pkgrel=1
epoch=1
pkgdesc="Next generation desktop shell"
url="https://wiki.gnome.org/Projects/GnomeShell"
arch=(x86_64)
license=(GPL)
depends=(accountsservice gcr-4 gjs upower gnome-session gtk4
         gnome-settings-daemon gsettings-desktop-schemas libcanberra-pulse
         libgdm libsecret mutter libnma unzip libibus gnome-autoar
         gnome-disk-utility libsoup3 libgweather-4)
makedepends=(gtk-doc gnome-control-center evolution-data-server
             gobject-introspection git meson sassc asciidoc bash-completion)
if [ -n "$_enable_check" ]; then
  checkdepends=(xorg-server-xvfb python-dbusmock xorg-server-xvfb)
fi
optdepends=('gnome-control-center: System settings'
            'evolution-data-server: Evolution calendar integration'
            'gst-plugins-good: Screen recording'
            'gst-plugin-pipewire: Screen recording'
            'gnome-bluetooth-3.0: Bluetooth support')
groups=(gnome)
provides=(gnome-shell gnome-shell=$pkgver gnome-shell=$epoch:$pkgver)
conflicts=(gnome-shell)
_commit=b0ca64e7775225b7c5d049571a44ef40bf516406  # tags/44.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "GS_transparency.patch"
        "unlockDialog_remove_blur.patch"
        "no_Overview_left_Clock.patch")
sha256sums=('SKIP'
            'SKIP'
            'eb8baf023efce79dc770c4a7fae403faff37bf20e08a14eb2f5a68811c3844cb'
            'f3b92285a9655df1c3bb5f31f4b3c589b4f0592f4314b1d6108e9d095e22a23b'
            'e57a0a1a6c91461e4b998da01d8458247990a8c0c919b9ad54d79375472ab3e6')

pkgver() {
  cd gnome-shell
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gnome-shell

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update
  
  patch -p1 < "$srcdir/no_Overview_left_Clock.patch"
  # patch -p1 < "$srcdir/GS_transparency.patch"
  # patch -p1 < "$srcdir/unlockDialog_remove_blur.patch"
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson $_pkgname build \
    -D gtk_doc=false \
    -D tests=false
  meson compile -C build
}

_check() (
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/rdir}"
  export XDG_RUNTIME_DIR

  meson test -C build --print-errorlogs -t 3
)

if [ -n "$_enable_check" ]; then
  check() {
    dbus-run-session xvfb-run -s '-nolisten local +iglx -noreset' \
      bash -c "$(declare -f _check); _check"
  }
fi

package() {
  depends+=(libmutter-12.so)
  install=gnome-shell.install
  meson install -C build --destdir "$pkgdir"
}

