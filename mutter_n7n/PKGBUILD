# n7n package:
# Maintainer: Jordan Johnston <johnstonljordan AT gmail DOT com>

# Official package:
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgbase=mutter_n7n
pkgname=(mutter_n7n mutter_n7n-docs)
pkgver=44.2
pkgrel=1
pkgdesc="A window manager for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64 aarch64)
license=(GPL)
depends=(dconf gobject-introspection-runtime gsettings-desktop-schemas
         libcanberra startup-notification libsm gnome-desktop-4 libxkbcommon-x11
         gnome-settings-daemon libgudev libinput pipewire xorg-xwayland graphene
         libxkbfile libsysprof-capture lcms2 colord)
makedepends=(gobject-introspection git egl-wayland meson xorg-server
             wayland-protocols sysprof gi-docgen)
_commit=e7ed2bf85700a2ff33b69826f6f0fff6e2f28e69  # tags/44.2^0
source=("git+https://gitlab.gnome.org/GNOME/mutter.git#commit=$_commit"
        '0001-tests-cogl-test-framebuffer-get-bits-should-fail-on-.patch'
        'mr1441.patch'
        'mr2269.patch'
        'mr2941.patch'
        'prio.patch')
sha256sums=('SKIP'
            '3321a1b16de808469333231096074e4caaca8563e0a138344246c9301dfac3b9'
            '3621acfed945773de4e107d4b077d8ff2b31eb4adc52d956ee6cc6bb245bac04'
            'b963fe9d6cf7eeed66b5909735dd930355b759feb25b99eab14c007351595fbf'
            '96900ec36b890183dc879c5658c4b2058b90b50e4a872470efaf21cf801ccb54'
            'cca15ee32b5b4d942960672ab37403b2ccf5d249711fc321b333c3936d9ab897')

pkgver() {
  cd mutter
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd mutter

  patch -p1 < "$srcdir/0001-tests-cogl-test-framebuffer-get-bits-should-fail-on-.patch"
  patch -p1 < "$srcdir/mr1441.patch"
  patch -p1 < "$srcdir/prio.patch"
  patch -p1 < "$srcdir/mr2269.patch"
  patch -p1 < "$srcdir/mr2941.patch"

}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson mutter build \
    -D egl_device=true \
    -D wayland_eglstream=true \
    -D docs=true \
    -D installed_tests=false
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mutter_n7n() {
  provides=(mutter libmutter-12.so)
  conflicts=(mutter)
  groups=(gnome)
  install=mutter.install

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/mutter-*/doc
}

package_mutter_n7n-docs() {
  provides=(mutter-docs)
  conflicts=(mutter-docs)
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}
