# n7n package:
# Maintainer: Jordan Johnston <johnstonljordan AT gmail DOT com>

# Official package:
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgbase=mutter_n7n
pkgname=(mutter_n7n mutter_n7n-docs)
pkgver=42.2
pkgrel=1
pkgdesc="A window manager for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64)
license=(GPL)
depends=(dconf gobject-introspection-runtime gsettings-desktop-schemas
         libcanberra startup-notification zenity libsm gnome-desktop 
         libxkbcommon-x11 gnome-settings-daemon libgudev libinput pipewire
         xorg-xwayland graphene libxkbfile libsysprof-capture)
makedepends=(gobject-introspection git egl-wayland meson xorg-server
             wayland-protocols sysprof gi-docgen)
checkdepends=(xorg-server-xvfb wireplumber python-dbusmock)
_commit=10cece870cf25c15daa8b20130d328257ec5f132  # tags/42.2^0
source=("git+https://gitlab.gnome.org/GNOME/mutter.git#commit=$_commit"
        'mutter-blur-rounded_42.patch'
        'max-rounded.patch'
        'mutter-sched_fifo.patch'
        'mr1441.patch'
        'mr1877.patch'
        'mr2091.patch')
sha256sums=('SKIP'
            '05232e60abda3b88691ff9066edd19fe65d984f5a5783023f7ac2e883b9113e6'
            '0c83c468c68ae7fc7c5273688ff35f41cc1ac77dd6b4b1d7b6b77cbafbead43f'
            '51cf9a9d2c345d34df0bea7d1fa3f2e3257f0e3576a18ff2d4d8b38b64204159'
            'c8e8e05f3b5ccf2faeeda45231bc12e4fff516c1e27f9d7a880a71eb00e5b0fa'
            '5284e17ac5cc129ecd21a0726d8159d44abc2e0da5dbcf195f2f05aab8e06120'
            '98e2e267e9519636d26173716168965fc1ecb7f791179e753573ad758c6b3849')

pkgver() {
  cd mutter
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd mutter

  patch -p1 < "$srcdir/mutter-blur-rounded_42.patch"
  patch -p1 < "$srcdir/max-rounded.patch"   
  patch -p1 < "$srcdir/mr1441.patch"
  patch -p1 < "$srcdir/mr1877.patch"
  patch -p1 < "$srcdir/mr2091.patch"
  patch -p1 < "$srcdir/mutter-sched_fifo.patch"
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition -flto=auto -flto-partition=one -fdevirtualize-at-ltrans"
  LDFLAGS+=" -Wl,-Bsymbolic-functions -flto=auto -flto-partition=one -fdevirtualize-at-ltrans"

  arch-meson mutter build \
    -D egl_device=true \
    -D wayland_eglstream=true \
    -D docs=true \
    -D installed_tests=false
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mutter_n7n() {
  provides=(mutter libmutter-10.so)
  conflicts=(mutter)
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/mutter-*/doc
}

package_mutter_n7n-docs() {
  provides=(mutter-docs)
  conflicts=(mutter-docs)
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}

# vim:set sw=2 et:
