# n7n package:
# Maintainer: Jordan Johnston <johnstonljordan AT gmail DOT com>

# Official package:
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgbase=mutter_n7n
pkgname=(mutter_n7n mutter_n7n-docs)
pkgver=43.2
pkgrel=7
pkgdesc="A window manager for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64)
license=(GPL)
depends=(dconf gobject-introspection-runtime gsettings-desktop-schemas
         libcanberra startup-notification libsm gnome-desktop libxkbcommon-x11
         gnome-settings-daemon libgudev libinput pipewire xorg-xwayland graphene
         libxkbfile libsysprof-capture lcms2 colord)
makedepends=(gobject-introspection git egl-wayland meson xorg-server
             wayland-protocols sysprof gi-docgen)
checkdepends=(xorg-server-xvfb wireplumber python-dbusmock zenity)
_commit=46f4143619734ec2b95503ba96e444f61f27e18e  # tags/43.2^0
source=("git+https://gitlab.gnome.org/GNOME/mutter.git#commit=$_commit"
        'mutter-sched_fifo.patch'
        'mr1441.patch'
        'mr1880.patch'
        'mr2091.patch'
        'mr2679.patch'
        'mr2508.patch'
        'mr2269.patch'
        'mr2677.patch'
        'mutter-blur-rounded_43_x11.patch'
        'max-rounded.patch'
        'mutter-sched_fifo.patch')
sha256sums=('SKIP'
            '51cf9a9d2c345d34df0bea7d1fa3f2e3257f0e3576a18ff2d4d8b38b64204159'
            'f43eb4bfa647ed5889a73821c704434c18a8b3750f44f7353914400e798a9377'
            '0a4dd1a4cae78085943f96e76f9e6d09aff55a71a8c484affb8e9a01c11e15b9'
            '98e2e267e9519636d26173716168965fc1ecb7f791179e753573ad758c6b3849'
            '042fc25e554d68957a02373eea33f7489c2e073658436452415903a427f19fc8'
            '83421c3a72787591390eb8907d5a653d63f460db5a5118e5a8b9eabbd36019ce'
            'b963fe9d6cf7eeed66b5909735dd930355b759feb25b99eab14c007351595fbf'
            'e419a200a11235c0f1ae1ecfe5f69581a99cb4a4061fd1cdaec8ab24294cfb2a'
            '3f741b52f92e127a36ded41da3bb8394d7833967cbbe6065171e10c8f4c6d704'
            '981b8316e6b8445f869aac52d8ad53d0cd7b7fbc488a6c66fe0f26f8276bcdc2'
            '51cf9a9d2c345d34df0bea7d1fa3f2e3257f0e3576a18ff2d4d8b38b64204159')

pkgver() {
  cd mutter
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd mutter

  msg2 "1"
  patch -p1 < "$srcdir/mr1441.patch"
  patch -p1 < "$srcdir/mr1880.patch"
  patch -p1 < "$srcdir/mr2091.patch"
  patch -p1 < "$srcdir/mr2679.patch"
  patch -p1 < "$srcdir/mr2508.patch"
  patch -p1 < "$srcdir/mutter-blur-rounded_43_x11.patch"
  patch -p1 < "$srcdir/max-rounded.patch"
  patch -p1 < "$srcdir/mutter-sched_fifo.patch"
  patch -p1 < "$srcdir/mr2269.patch" 
  patch -p1 < "$srcdir/mr2677.patch"
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"

  arch-meson mutter build \
    -D egl_device=true \
    -D wayland_eglstream=true \
    -D docs=true \
    -D installed_tests=false
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_mutter_n7n() {
  provides=(mutter libmutter-11.so)
  conflicts=(mutter)
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/mutter-*/doc
}

package_mutter_n7n-docs() {
  provides=(mutter-docs)
  conflicts=(mutter-docs)
  pkgdesc+=" (documentation)"
  depends=()

  mv docs/* "$pkgdir"
}

# vim:set sw=2 et:
